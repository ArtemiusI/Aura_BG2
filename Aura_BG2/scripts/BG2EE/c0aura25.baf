IF
Global("C0AuraSummoned","GLOBAL",1)
THEN
RESPONSE #100
Wait(1)
StartDialogueNoSet([PC])
END

// SUNSHOOTER

IF
	LevelGT(Myself,5)
	OR(2)
	HasItemEquiped("c0aubo1a",Myself)
	HasItemEquiped("c0aubo1b",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter II.)~ [GAM_11])
	TransformItem("c0aubo1a","c0aubo2a")
	TransformItem("c0aubo1b","c0aubo2b")
	EquipRanged()
END

IF
	LevelGT(Myself,8)
	OR(3)
	HasItemEquiped("c0aubo2a",Myself)
	HasItemEquiped("c0aubo2b",Myself)
	HasItemEquiped("c0aubo2c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter III.)~ [GAM_11])
	TransformItem("c0aubo2a","c0aubo3a")
	TransformItem("c0aubo2b","c0aubo3b")
	TransformItem("c0aubo2c","c0aubo3c")
	EquipRanged()
END

IF
	LevelGT(Myself,11)
	OR(3)
	HasItemEquiped("c0aubo3a",Myself)
	HasItemEquiped("c0aubo3b",Myself)
	HasItemEquiped("c0aubo3c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter IV.)~ [GAM_11])
	TransformItem("c0aubo3a","c0aubo4a")
	TransformItem("c0aubo3b","c0aubo4b")
	TransformItem("c0aubo3c","c0aubo4c")
	EquipRanged()
END

IF
	LevelGT(Myself,16)
	OR(3)
	HasItemEquiped("c0aubo4a",Myself)
	HasItemEquiped("c0aubo4b",Myself)
	HasItemEquiped("c0aubo4c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter V.)~ [GAM_11])
	TransformItem("c0aubo4a","c0aubo5a")
	TransformItem("c0aubo4b","c0aubo5b")
	TransformItem("c0aubo4c","c0aubo5c")
	EquipRanged()
END

IF
	Global("C0AUIN6","LOCALS",1)
	OR(3)
	HasItemEquiped("c0aubo5a",Myself)
	HasItemEquiped("c0aubo5b",Myself)
	HasItemEquiped("c0aubo5c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Grand Artifice: Aura has upgraded her bow to the Perfect Sunshooter.)~ [GAM_11])
	TransformItem("c0aubo5a","c0aubo6")
	TransformItem("c0aubo5b","c0aubo6")
	TransformItem("c0aubo5c","c0aubo6")
	EquipRanged()
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!InMyArea([ENEMY])
    AreaCheck("AR4500")
	CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
    GlobalGT("SarevokAlive","GLOBAL",3)
	Global("C0AuraToBPocketPlane","GLOBAL",0)
  THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
      Wait(2)
	  SetGlobal("C0AuraToBPocketPlane","GLOBAL",1)
	  StartDialogNoSet(Player1)
	  SetInterrupt(TRUE)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!InMyArea([ENEMY])
    AreaCheck("AR4500")
	See("LeavePocketPlane")
	CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
    Global("ChallengesDone","GLOBAL",1)
	Global("C0AuraImoenToBFlirt","GLOBAL",1)
	GlobalLT("C0AuraImoenToB1","GLOBAL",4)
  THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
      Wait(2)
	  SetGlobal("C0AuraImoenToB1","GLOBAL",4)
	  Interact("Imoen2")
	  SetInterrupt(TRUE)
END

IF
    InParty(Myself)
	Global("C0AuraRomanceActive","GLOBAL",2)
    Global("C0AuraToBLoveTalk","GLOBAL",0)
	GlobalGT("MelissanJob","GLOBAL",0)
THEN
  RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",1)
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",FIVE_ROUNDS)
END

IF
    InParty(Myself)
    RealGlobalTimerExpired("C0AuraBG2RomanceTimer","GLOBAL")
	Global("C0AuraRomanceActive","GLOBAL",2)
    !AreaType(DUNGEON)
    See(Player1)
    !See([ENEMY])
    !Range([NEUTRAL],10)
    CombatCounter(0)
	GlobalGT("MelissanJob","GLOBAL",0)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",2)
THEN
    RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",2)
		PlaySong(%C0AUROM%)
        Interact(Player1)
END

IF
    InParty(Myself)
	Global("C0AuraRomanceActive","GLOBAL",2)
	GlobalGT("C0AuraToBRomWraith","GLOBAL",0)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",3)
THEN
  RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",3)
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",FIVE_ROUNDS)
END

IF
    InParty(Myself)
    RealGlobalTimerExpired("C0AuraBG2RomanceTimer","GLOBAL")
	Global("C0AuraRomanceActive","GLOBAL",2)
    !AreaType(DUNGEON)
    See(Player1)
    !See([ENEMY])
    !Range([NEUTRAL],10)
    CombatCounter(0)
	GlobalGT("C0AuraToBRomWraith","GLOBAL",0)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",4)
THEN
    RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",4)
		PlaySong(%C0AUROM%)
        Interact(Player1)
END

IF
    InParty(Myself)
	Global("C0AuraRomanceActive","GLOBAL",2)
	Global("SaradushSacked","GLOBAL",1)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",5)
THEN
  RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",5)
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",FIVE_ROUNDS)
END

IF
    InParty(Myself)
    RealGlobalTimerExpired("C0AuraBG2RomanceTimer","GLOBAL")
	Global("C0AuraRomanceActive","GLOBAL",2)
    !AreaType(DUNGEON)
    See(Player1)
    !See([ENEMY])
    !Range([NEUTRAL],10)
    CombatCounter(0)
	Global("SaradushSacked","GLOBAL",1)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",6)
THEN
    RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",6)
		PlaySong(%C0AUROM%)
        Interact(Player1)
END

IF
    InParty(Myself)
	Global("C0AuraRomanceActive","GLOBAL",2)
	GlobalGT("Chapter","GLOBAL",%bg2_chapter_8%)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",7)
THEN
  RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",7)
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",FIVE_ROUNDS)
END

IF
    InParty(Myself)
    RealGlobalTimerExpired("C0AuraBG2RomanceTimer","GLOBAL")
	Global("C0AuraRomanceActive","GLOBAL",2)
    !AreaType(DUNGEON)
    See(Player1)
    !See([ENEMY])
    !Range([NEUTRAL],10)
    CombatCounter(0)
	GlobalGT("Chapter","GLOBAL",%bg2_chapter_8%)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",8)
THEN
    RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",8)
		PlaySong(%C0AUROM%)
        Interact(Player1)
END


IF
    InParty(Myself)
    RealGlobalTimerExpired("C0AuraBG2RomanceTimer","GLOBAL")
	Global("C0AuraRomanceActive","GLOBAL",2)
    !AreaType(DUNGEON)
    See(Player1)
    !See([ENEMY])
    !Range([NEUTRAL],10)
    CombatCounter(0)
	Switch("C0AuraToBLoveTalk","GLOBAL")
THEN
    RESPONSE #8
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",3600)
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
    RESPONSE #9
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
		PlaySong(%C0AUROM%)
        Interact(Player1)
    RESPONSE #10
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",3600)
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
    RESPONSE #11
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
		PlaySong(%C0AUROM%)
        Interact(Player1)
    RESPONSE #12
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",3600)
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
    RESPONSE #13
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
		PlaySong(%C0AUROM%)
        Interact(Player1)
    RESPONSE #14
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",3600)
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
    RESPONSE #15
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
		PlaySong(%C0AUROM%)
        Interact(Player1)
    RESPONSE #16
        IncrementGlobal("C0AuraToBLoveTalk","GLOBAL",1)
END

IF
    InParty(Myself)
	Global("C0AuraRomanceActive","GLOBAL",2)
	Global("C0AuraAMELM0116","GLOBAL",1)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",17)
THEN
  RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",17)
        RealSetGlobalTimer("C0AuraBG2RomanceTimer","GLOBAL",FIVE_ROUNDS)
END

IF
    InParty(Myself)
    RealGlobalTimerExpired("C0AuraBG2RomanceTimer","GLOBAL")
	Global("C0AuraRomanceActive","GLOBAL",2)
    !AreaType(DUNGEON)
    See(Player1)
    !See([ENEMY])
    !Range([NEUTRAL],10)
    CombatCounter(0)
	Global("C0AuraAMELM0116","GLOBAL",1)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",18)
THEN
    RESPONSE #100
        SetGlobal("C0AuraToBLoveTalk","GLOBAL",18)
		PlaySong(%C0AUROM%)
        Interact(Player1)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!InMyArea([ENEMY])
    AreaCheck("AR4500")
	NearLocation(Player1,1985,1150,15)
	CombatCounter(0)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
    Global("ChallengesDone","GLOBAL",1)
	Global("C0AuraRomanceActive","GLOBAL",2)
	GlobalLT("C0AuraToBLoveTalk","GLOBAL",20)
  THEN
	RESPONSE #100
	  SetInterrupt(FALSE)
  	  SetGlobal("C0AuraToBLoveTalk","GLOBAL",20)
	  ClearAllActions()
	  StartCutSceneMode()
      Wait(2)
	  PlaySong(%C0AUROM%)
	  MoveToObject(Player1)
	  Wait(1)
	  ActionOverride(Player1,StartDialog("player1",Player1))
	  SetInterrupt(TRUE)
END

IF
    InParty(Myself)
	Global("C0AuraRomanceActive","GLOBAL",2)
    Global("CandleYoshimo","GLOBAL",10)
    Global("C0AuraToBYRDreamTalk","GLOBAL",0)
THEN
  RESPONSE #100
        SetGlobal("C0AuraToBYRDreamTalk","GLOBAL",1)
		PlaySong(%C0AUROM%)
        Interact(Player1)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraToBBowstring","GLOBAL",0)
	PartyHasItem("COMPON10")
    !See([ENEMY])
	CombatCounter(0)
	See(Player1)
THEN
  RESPONSE #100
	SetInterrupt(FALSE)
	Wait(2)
	SetGlobal("C0AuraToBBowstring","GLOBAL",1)
	SetInterrupt(TRUE)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraPerfectSunshooter","GLOBAL",0)
	PartyHasItem("c0aubo6")
    !See([ENEMY])
	CombatCounter(0)
	See(Player1)
THEN
  RESPONSE #100
	SetInterrupt(FALSE)
	Wait(3)
	SetGlobal("C0AuraPerfectSunshooter","GLOBAL",1)
	SetInterrupt(TRUE)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("Spelunking","GLOBAL",1)
	Global("C0AuraAbazigalWater","GLOBAL",0)
    AreaCheck("AR6001")
    !See([ENEMY])
	CombatCounter(0)
	See(Player1)
THEN
  RESPONSE #100
	SetInterrupt(FALSE)
	Wait(1)
	SetGlobal("C0AuraAbazigalWater","GLOBAL",1)
	SetInterrupt(TRUE)
	StartDialogueNoSet(Player1)
END
