// RANDOM WALK

IF
  AreaCheck("C0AU01")
  !InParty(Myself)
  Global("KickedOut","LOCALS",1)
THEN
  RESPONSE #100
    RandomWalk()
END

IF
  OR(12)
    Allegiance("majan",ENEMY)
    Allegiance("gerhardt",ENEMY)
    Allegiance("lissa",ENEMY)
    Allegiance("jaella",ENEMY)
    Allegiance("Kylie",ENEMY)
    Dead("lissa")
    Dead("jaella")
    Dead("majan")
    Dead("gerhardt")
    Dead("tat")
    Dead("tot")
    Dead("Kylie")
  InParty(Myself)
  Global("C0AuraHostile","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("C0AuraHostile","LOCALS",1)
    DisplayStringHead(Myself,~What have you done...? WHAT HAVE YOU DONE?~)
    LeaveParty()
    Enemy()
END

IF
  OR(12)
    Allegiance("majan",ENEMY)
    Allegiance("gerhardt",ENEMY)
    Allegiance("lissa",ENEMY)
    Allegiance("jaella",ENEMY)
    Allegiance("Kylie",ENEMY)
    Dead("lissa")
    Dead("jaella")
    Dead("majan")
    Dead("gerhardt")
    Dead("tat")
    Dead("tot")
    Dead("Kylie")
	Allegiance(Myself,NEUTRAL)
  Global("C0AuraHostile","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("C0AuraHostile","LOCALS",1)
    DisplayStringHead(Myself,~What have you done...? WHAT HAVE YOU DONE?~)
    Enemy()
END

// EXPERIENCE

IF
  Allegiance(Myself,ENEMY)
  !InParty(Myself)
  Global("NPCFight","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("NPCFight","LOCALS",1)
    ChangeAIScript("",DEFAULT)
    ChangeAIScript("",GENERAL)
    ChangeAIScript("c0aurax",SPECIFICS)
    SetDialog("")
    Continue()
END

IF
  Allegiance(Myself,NEUTRAL)
  AttackedBy([GOODCUTOFF],DEFAULT)
THEN
  RESPONSE #100
    ChangeAIScript("c0aurax",SPECIFICS)
    ChangeAIScript("",GENERAL)
    ChangeAIScript("",DEFAULT)
    Enemy()
    Continue()
END

IF
  !Allegiance(Myself,ENEMY)
  !InParty(Myself)
  Global("NPCFight","LOCALS",1)
THEN
  RESPONSE #100
    SetGlobal("NPCFight","LOCALS",0)
    ChangeAIScript("",GENERAL)
    ChangeAIScript("",SPECIFICS)
    Continue()
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  Class(Myself,MAGE_ALL)
  XPGT(Player1,1249999)
  XPLT(Myself,1250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1250000,SET)
	AddSpecialAbility("spwi415")
	AddSpecialAbility("spwi417")
	AddSpecialAbility("spwi510")
	AddSpecialAbility("spwi505")
	AddSpecialAbility("spwi513")
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  !Class(Myself,MAGE_ALL)
  XPGT(Player1,1249999)
  XPLT(Myself,1250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1250000,SET)
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  Class(Myself,MAGE_ALL)
  XPGT(Player1,999999)
  XPLT(Myself,1000000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1000000,SET)
	AddSpecialAbility("spwi415")
	AddSpecialAbility("spwi417")
	AddSpecialAbility("spwi510")
	AddSpecialAbility("spwi505")
	AddSpecialAbility("spwi513")
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  !Class(Myself,MAGE_ALL)
  XPGT(Player1,999999)
  XPLT(Myself,1000000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,1000000,SET)
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  Class(Myself,MAGE_ALL)
  XPGT(Player1,749999)
  XPLT(Myself,750000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,750000,SET)
	AddSpecialAbility("spwi415")
	AddSpecialAbility("spwi417")
	AddSpecialAbility("spwi510")
	AddSpecialAbility("spwi505")
	AddSpecialAbility("spwi513")
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  !Class(Myself,MAGE_ALL)
  XPGT(Player1,749999)
  XPLT(Myself,750000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,750000,SET)
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  Class(Myself,MAGE_ALL)
  XPGT(Player1,499999)
  XPLT(Myself,500000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,500000,SET)
	AddSpecialAbility("spwi415")
	AddSpecialAbility("spwi417")
	AddSpecialAbility("spwi510")
	AddSpecialAbility("spwi505")
	AddSpecialAbility("spwi513")
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  !Class(Myself,MAGE_ALL)
  XPGT(Player1,499999)
  XPLT(Myself,500000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,500000,SET)
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,249999)
  XPLT(Myself,250000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,250000,SET)
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPGT(Player1,124999)
  XPLT(Myself,125000)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
    ChangeStat(Myself,XP,125000,SET)
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

IF
  Global("BD_JOINXP","LOCALS",0)
  InParty(Myself)
  XPLT(Player1,89001)
THEN
  RESPONSE #100
    SetInterrupt(FALSE)
    SetGlobal("BD_JOINXP","LOCALS",1)
	SetGlobal("C0AlchemyRefreshStock","GLOBAL",1)
    SetInterrupt(TRUE)
END

// Friendship

IF
    InParty(Myself)
	Global("C0AuraAutocannonStart","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C0AuraAutocannonStartRealTimer","GLOBAL",TWO_HOURS)
    SetGlobalTimer("C0AuraAutocannonStartTimer","GLOBAL",FIVE_DAYS)
    SetGlobal("C0AuraAutocannonStart","GLOBAL",1)
END

IF
    InParty(Myself)
	Global("C0AuraKnowsBG1","GLOBAL",1)
	Global("C0AuraBG2FriendshipImportTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C0AuraTalkRealTimer","GLOBAL",ONE_HOUR)
    SetGlobalTimer("C0AuraTalkTimer","GLOBAL",ONE_DAY)
    SetGlobal("C0AuraBG2FriendshipImportTalk","GLOBAL",1)
END

IF
    InParty(Myself)
	!Global("C0AuraKnowsBG1","GLOBAL",1)
	Global("C0AuraBG2FriendshipTalk","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("C0AuraTalkRealTimer","GLOBAL",ONE_HOUR)
    SetGlobalTimer("C0AuraTalkTimer","GLOBAL",ONE_DAY)
    SetGlobal("C0AuraBG2FriendshipTalk","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0AuraTalkRealTimer","GLOBAL")
    GlobalTimerExpired("C0AuraTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!Global("C0AuraTalkStopped","GLOBAL",1)
	GlobalLT("C0AuraInsulted","GLOBAL",2)
    Switch("C0AuraBG2FriendshipImportTalk","GLOBAL")
THEN
  RESPONSE #1
    IncrementGlobal("C0AuraBG2FriendshipImportTalk","GLOBAL",1)
  RESPONSE #2
	StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0AuraTalkRealTimer","GLOBAL")
    GlobalTimerExpired("C0AuraTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!Global("C0AuraTalkStopped","GLOBAL",1)
	GlobalLT("C0AuraInsulted","GLOBAL",2)
	HPPercentLT(Myself,76)
    Switch("C0AuraBG2FriendshipImportTalk","GLOBAL")
THEN
  RESPONSE #3
    IncrementGlobal("C0AuraBG2FriendshipImportTalk","GLOBAL",1)
  RESPONSE #4
	StartDialogueNoSet(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck(Myself,CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("C0AuraTalkRealTimer","GLOBAL")
    GlobalTimerExpired("C0AuraTalkTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
	!Global("C0AuraTalkStopped","GLOBAL",1)
	GlobalLT("C0AuraInsulted","GLOBAL",2)
    Switch("C0AuraBG2FriendshipTalk","GLOBAL")
THEN
  RESPONSE #1
    IncrementGlobal("C0AuraBG2FriendshipTalk","GLOBAL",1)
END


// Romance

IF
	Happiness(Myself,-299)
	OR(2)
	Global("C0AuraBG2RomanceActive","GLOBAL",1)
	Global("C0AuraBG2RomanceActive","GLOBAL",2)
THEN
  RESPONSE #100
	SetGlobal("C0AuraBG2RomanceActive","GLOBAL",3)
END

// I HATE BEARS

IF
	InParty(Myself)
	Global("C0AuraHatesBears","GLOBAL",0)
	See([0.0.BEAR])
THEN
  RESPONSE #100
  SetGlobal("C0AuraHatesBears","GLOBAL",1)
  StartDialogueNoSet(Player1)
END

IF
	CombatCounter(0)
	ActionListEmpty()
	InParty(Myself)
	Range("wilson",5)
THEN
RESPONSE #100
	RunAwayFrom("wilson",15)
END

// AUTOMATON - HELP

IF
	See("c0auin3")
    AttackedBy([ENEMY],DEFAULT)
	HPPercentLT(Myself,50)
	Global("Help","LOCALS",0)
  THEN
    RESPONSE #50
      DisplayStringHead(Myself,~Help!~)
      ActionOverride("c0auin3",AttackReevaluate(Lastattackerof("c0aura"),15))
	  SetGlobal("Help","LOCALS",1)
	  SetGlobalTimer("HelpTimer","LOCALS",12)
END

IF
	Global("Help","LOCALS",1)
	GlobalTimerExpired("HelpTimer","LOCALS")
THEN
    RESPONSE #50
	 SetGlobal("Help","LOCALS",0)
END

// SUNSHOOTER

// SUNSHOOTER

IF
	LevelGT(Myself,5)
	OR(3)
	HasItemEquiped("c0aubo1a",Myself)
	HasItemEquiped("c0aubo1b",Myself)
	HasItemEquiped("c0aubo1c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter II.)~ [GAM_11])
	TransformItem("c0aubo1a","c0aubo2a")
	TransformItem("c0aubo1b","c0aubo2b")
	TransformItem("c0aubo1c","c0aubo2c")
	EquipRanged()
END

IF
	LevelGT(Myself,8)
	OR(3)
	HasItemEquiped("c0aubo2a",Myself)
	HasItemEquiped("c0aubo2b",Myself)
	HasItemEquiped("c0aubo2c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter III.)~ [GAM_11])
	TransformItem("c0aubo2a","c0aubo3a")
	TransformItem("c0aubo2b","c0aubo3b")
	TransformItem("c0aubo2c","c0aubo3c")
	EquipRanged()
END

IF
	LevelGT(Myself,11)
	OR(3)
	HasItemEquiped("c0aubo3a",Myself)
	HasItemEquiped("c0aubo3b",Myself)
	HasItemEquiped("c0aubo3c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter IV.)~ [GAM_11])
	TransformItem("c0aubo3a","c0aubo4a")
	TransformItem("c0aubo3b","c0aubo4b")
	TransformItem("c0aubo3c","c0aubo4c")
	EquipRanged()
END

IF
	LevelGT(Myself,16)
	OR(3)
	HasItemEquiped("c0aubo4a",Myself)
	HasItemEquiped("c0aubo4b",Myself)
	HasItemEquiped("c0aubo4c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Aura has upgraded her bow to Sunshooter V.)~ [GAM_11])
	TransformItem("c0aubo4a","c0aubo5a")
	TransformItem("c0aubo4b","c0aubo5b")
	TransformItem("c0aubo4c","c0aubo5c")
	EquipRanged()
END

IF
	Global("C0AUIN6","LOCALS",1)
	OR(3)
	HasItemEquiped("c0aubo5a",Myself)
	HasItemEquiped("c0aubo5b",Myself)
	HasItemEquiped("c0aubo5c",Myself)
THEN
RESPONSE #100
	DisplayStringHead(Myself,~(Grand Artifice: Aura has upgraded her bow to the Perfect Sunshooter.)~ [GAM_11])
	TransformItem("c0aubo5a","c0aubo6")
	TransformItem("c0aubo5b","c0aubo6")
	TransformItem("c0aubo5c","c0aubo6")
	EquipRanged()
END
///////////////////////////////
/////////INTERJECTIONS/////////
///////////////////////////////

// Tanner's basement

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraAutocannon","GLOBAL",0)
	CombatCounter(0)
	AreaCheck("C0AU01")
	See(Player1)
THEN
  RESPONSE #100
	SetGlobal("C0AuraAutocannon","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Tanner's basement

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraTanner","GLOBAL",0)
	CombatCounter(0)
	AreaCheck("AR0502")
	See(Player1)
THEN
  RESPONSE #100
	SetGlobal("C0AuraTanner","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraKylie","GLOBAL",0)
	CombatCounter(0)
	See("Kylie")
THEN
  RESPONSE #100
	ActionOverride("Kylie",FaceObject("C0Aura")
	ActionOverride("Kylie",Wait(1))
	ActionOverride("Kylie",StartDialogueNoSet("C0Aura"))
END

// WILSON!

IF
	InParty(Myself)
	InParty("Wilson")
	!Dead("Wilson")
	OR(2)
	Global("C0AuraWilsonConflict","GLOBAL",0)
	RealGlobalTimerExpired("C0AuraWilsonConflictTimer","GLOBAL")
	Switch("C0AuraWilsonConflict","GLOBAL")
THEN
  RESPONSE #0
	RealSetGlobalTimer("C0AuraWilsonConflictTimer","GLOBAL",FIVE_MINUTES)
	SetGlobal("C0AuraWilsonConflict","GLOBAL",1)
  RESPONSE #1
	SetGlobal("C0AuraWilsonConflict","GLOBAL",2)
	StartDialogueNoSet(Player1)
END

// Trolls

IF
	Delay(2)
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("ar1302")
	Global("C0AuraDeArniseKeep","GLOBAL",0)
	CombatCounter(0)
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END

IF
	Delay(2)
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("ar1900")
	Global("C0AuraDruidGrove","GLOBAL",0)
	CombatCounter(0)
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END

// Flying Machine

IF
	InParty(Myself)
	InParty("JAN")
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck("JAN",CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	See("Flying Machine 1")
	Global("C0AuraJanAirship","GLOBAL",0)
	CombatCounter(0)
	Delay(2)
THEN
  RESPONSE #100
	ActionOverride("JAN",Interact("C0AURA"))
END

// Docks

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	AreaCheck("AR0300")
	Global("C0AuraDocksDistrict","GLOBAL",0)
	CombatCounter(0)
THEN
  RESPONSE #100
	Wait(2)
	SetGlobal("C0AuraDocksDistrict","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Neb

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraNebEncounter","GLOBAL",1)
	!See([ENEMY])
THEN
  RESPONSE #100
	StartDialogueNoSet(Player1)
END

// Elephant Skin

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	PartyHasItem("misc5g")
	Global("MerchantHide","GLOBAL",0)
	Global("C0AuraElephantSkin","GLOBAL",0)
	CombatCounter(0)
	Delay(2)
THEN
  RESPONSE #100
	SetGlobal("C0AuraElephantSkin","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Tanner's basement

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraTanner","GLOBAL",0)
	Global("EnteredTanner","AR0502",0)
	CombatCounter(0)
	AreaCheck("AR0502")
	See(Player1)
THEN
  RESPONSE #100
	SetGlobal("C0AuraTanner","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Dead Mimic

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Dead("jugmim01")
	AreaCheck("AR1106")
	Global("C0AuraMimic","GLOBAL",0)
	CombatCounter(0)
THEN
  RESPONSE #100
	SetGlobal("C0AuraMimic","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	Dead("jugmim01")
	!AreaCheck("AR1106")
	Global("C0AuraMimic","GLOBAL",0)
THEN
  RESPONSE #100
	SetGlobal("C0AuraMimic","GLOBAL",-1)
END

// Orcish Cookbook

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Dead("firtrl01")
	PartyHasItem("book93")
	Global("C0AuraOrcishCookbook","GLOBAL",0)
	CombatCounter(0)
	Delay(4)
THEN
  RESPONSE #100
	SetGlobal("C0AuraOrcishCookbook","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

// Illithium

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraIllithium","GLOBAL",0)
	PartyHasItem("misc6s")
	CombatCounter(0)
THEN
  RESPONSE #100
	SetInterrupt(FALSE)
	Wait(2)
	SetGlobal("C0AuraIllithium","GLOBAL",1)
	StartDialogueNoSet(Player1)
	SetInterrupt(TRUE)
END

// Autocannon

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("C0AuraAutocannonStart","GLOBAL",1)
    RealGlobalTimerExpired("C0AuraAutocannonStartRealTimer","GLOBAL")
    GlobalTimerExpired("C0AuraAutocannonStartTimer","GLOBAL")
	CombatCounter(0)
THEN
  RESPONSE #100
	SetInterrupt(FALSE)
	SetGlobal("C0AuraAutocannonStart","GLOBAL",2)
	CreateCreatureObjectOffset("c0apico",Myself,[100.100])
	ActionOverride("c0apico",Dialogue("C0Aura")
	SetInterrupt(TRUE)
END

// Lavok

IF
	InParty(Myself)
	InMyArea(Player1)
	!StateCheck(Myself,CD_STATE_NOTVALID)
	!StateCheck(Player1,CD_STATE_NOTVALID)
	Global("ToldDemonsHeart","GLOBAL",1)
	See("LAVOK02")
	PartyHasItem("c0abook1")
	Global("C0AuraLavok","GLOBAL",0)
	CombatCounter(0)
THEN
  RESPONSE #100
	SetInterrupt(FALSE)
	Wait(2)
	SetGlobal("C0AuraLavok","GLOBAL",1)
	StartDialogueNoSet(Player1)
	SetInterrupt(TRUE)
END

// low rep

IF
	InParty(Myself)
    ReputationLT(Player1,8)
	Global("C0AuraUnhappy","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(2)
	SetGlobal("C0AuraUnhappy","GLOBAL",1)
	StartDialogueNoSet(Player1)
END

IF
	InParty(Myself)
    ReputationLT(Player1,4)
	Global("C0AuraLeaves","GLOBAL",0)
THEN
  RESPONSE #100
	Wait(2)
	SetGlobal("C0AuraLeaves","GLOBAL",1)
	StartDialogueNoSet(Player1)
END